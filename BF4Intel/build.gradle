apply plugin: 'com.android.application'

project.archivesBaseName = 'com.ninetwozero.bf4intel'

repositories {
    mavenCentral()
    maven {
        url "https://mint.splunk.com/gradle/"
    }
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId 'com.ninetwozero.bf4intel'
        versionCode 1614122
        versionName '1.2.2'
        minSdk 14
        targetSdk 31
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        bf4intel {
            storeFile file(System.properties['bf4intel.KeyPath'] ?: "${System.properties['user.home']}/.android/debug.keystore")
            storePassword = System.properties['bf4intel.StorePassword'] ?: "android"
            keyAlias = System.properties['bf4intel.KeyAlias'] ?: "androiddebugkey"
            keyPassword = System.properties['bf4intel.KeyPassword'] ?: "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField "boolean", "isDebug", "true"
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.bf4intel
            minifyEnabled false
            buildConfigField "boolean", "isDebug", "false"
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file("../ci-configs/lint-ignore.xml")
        xmlReport true
        xmlOutput file("build/lint/lint-report.xml")
        htmlReport false
    }
}

dependencies {
    implementation project(':core')
    implementation 'com.android.support:support-v4:28.0.0',
            'com.android.support:appcompat-v7:28.0.0',
            'com.android.support:cardview-v7:28.0.0',
            'com.android.volley:volley:1.2.1',
            'com.squareup:otto:1.3.8',
            'com.google.code.gson:gson:2.8.2',
            'com.squareup.picasso:picasso:2.5.2',
            'com.astuetz:pagerslidingtabstrip:1.0.1',
            'com.splunk.mint:mint:5.0.0',
            'se.emilsjolander:sprinkles:1.3.1'
    implementation files('libs/libGoogleAnalyticsServices.jar')
    implementation files('libs/httpclient-4.4.jar')
}
