buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.4'
    }
}
apply plugin: 'com.android.application'

project.archivesBaseName = 'com.ninetwozero.bf4intel'

repositories {
    mavenCentral()
    maven {
        url "https://mint.splunk.com/gradle/"
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId 'com.ninetwozero.bf4intel'
        versionCode 1614110
        versionName '1.1.0'
        minSdkVersion 14
        targetSdkVersion 21
    }

    signingConfigs {
        bf4intel {
            storeFile file(System.properties['bf4intel.KeyPath'] ?: "${System.properties['user.home']}/.android/debug.keystore")
            storePassword = System.properties['bf4intel.StorePassword'] ?: "android"
            keyAlias = System.properties['bf4intel.KeyAlias'] ?: "androiddebugkey"
            keyPassword = System.properties['bf4intel.KeyPassword'] ?: "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField "boolean", "isDebug", "true"
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.bf4intel
            minifyEnabled false
            buildConfigField "boolean", "isDebug", "false"
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file("../ci-configs/lint-ignore.xml")
        xmlReport true
        xmlOutput file("build/lint/lint-report.xml")
        htmlReport false
    }
}

dependencies {
    compile project(':core')
    compile 'com.android.support:support-v4:21.0.+',
            'com.android.support:appcompat-v7:21.0.+',
            'com.mcxiaoke.volley:library:1.0.8',
            'com.squareup:otto:1.3.5',
            'com.google.code.gson:gson:2.3.1',
            'com.squareup.picasso:picasso:2.4.0',
            'com.astuetz:pagerslidingtabstrip:1.0.1',
            'com.splunk.mint:mint:4.0.4',
            'se.emilsjolander:sprinkles:1.3.1'
    compile files('libs/libGoogleAnalyticsServices.jar')
}
